\***************************************************
\               File Description Comments
\***************************************************

\ Lever Habituation by SeÃ¡n Martin on 11/07/2019
    \ Brief Description: Drop a pellet every 60 seconds after collecting reward. After 3 presses on the same lever, that lever is retracted until the other lever is pressed.
    \ Ending criteria: Ends after 30mins, or after 90 pellets.
    \ Locked pins: Remember to turn off the fan (16) 

\***************************************************
\               Constant Definitions
\***************************************************
\ Experiment variable locations
    ^MaxTimeIdx = 0
    ^RateDropIdx = 1
    ^MaxPelletIdx = 2
    ^RequiredPressIdx = 3

\ Index holder locations
    ^FeederTimeIdx = 0
    ^NosepokeTimeIdx = 1
    ^LeftLeverTimeIdx = 2
    ^RightLeverTimeIdx = 3
    ^WrongLeftTimeIdx = 4
    ^WrongRightTimeIdx = 5
    ^WrongNosepokeTimeIdx = 6

\ Counter locations
    ^FeederCountIdx = 0
    ^NosepokeCountIdx = 1
    ^LeftLeverCountIdx = 2
    ^RightLeverCountIdx = 3
    ^WrongLeftCountIdx = 4
    ^WrongRightCountIdx = 5
    ^WrongNosepokeCountIdx = 6

\ Inputs
    ^LeftLever = 1
    ^RightLever = 3
    ^Nosepoke = 7

\ Outputs
    ^LeftLeverOut = 1
    ^RightLeverOut = 2
    ^LeftLight = 4
    ^RightLight = 5
    ^RewardLight = 6
    ^HouseLight = 7
    ^Feeder = 9
    ^FanBox = 16

\***************************************************
\               Variable Descriptions
\***************************************************

\ Aliases
    VAR_ALIAS Maximum Time (min) = A(0) \ Default 30 minutes
    VAR_ALIAS Drop Rate (secs) = A(1) \ Default 300 seconds
    VAR_ALIAS Max Pellets = A(2) \ Default 90
    VAR_ALIAS Presses to advance = A(3) \ Default 90

\ Arrays
    \ A = Experiment variables
    \ B = Index holders
    \ C = Counters
    \ D = Data array for feeder drop times
    \ E = Data array for nosepoke times
    \ L = Data array for left lever response times
    \ M = Data array for unnecessary left response times
    \ N = Data array for unnecessary right response times
    \ O = Data array for unnecessary nosepoke times
    \ R = Data array for right lever response times

    DIM A = 3
    DIM B = 6
    DIM C = 6
    DIM D = 120 \ Should be large enough to hold max_pellets + 1
    DIM E = 120 \ Should be large enough to hold max_pellets + 1
    DIM L = 120 \ Should be large enough to hold max_pellets + 1
    DIM M = 100000 \ Could be anything
    DIM N = 100000 \ Could be anything
    DIM O = 100000 \ Could be anything
    DIM R = 120 \ Should be large enough to hold max_pellets + 1

\ Counter descriptions
    \ C(0) = Number of Pellets dropped
    \ C(1) = Number of Nosepokes
    \ C(2) = Number of left lever presses
    \ C(3) = Number of right lever presses
    \ C(4) = Number of unnecessary left lever presses
    \ C(5) = Number of unnecessary right lever presses
    \ C(6) = Number of unnecessary nosepokes

\ Z Pulses
    \ Z1 = Screen update pulse
    \ Z2 = Activate Feeder
    \ Z3 = Left Lever Pressed
    \ Z4 = Right Lever Pressed
    \ Z32 = End the session

\ Single Variables
    \ P = Should pass test (1 = Yes, 0 = No)
    \ T = Elapsed time
    \ S = Time of last lever press

\ List of Variables to save at end of trial
    DISKVARS = A, B, C, D, E, L, M, N, O, P, R, T

\***************************************************
\               Implementation
\***************************************************

S.S.1, \ Control flow

    S1, \ Establish default values and turn on fan
        0.01":
            SET A(^MaxTimeIdx) = 30;
            SET A(^RateDropIdx) = 300;
            SET A(^MaxPelletIdx) = 90;
            SET A(^RequiredPressIdx) = 90;
            SET D(0) = -987.987;
            SET E(0) = -987.987;
            SET L(0) = -987.987;
            SET M(0) = -987.987;
            SET N(0) = -987.987;
            SET O(0) = -987.987;
            SET R(0) = -987.987;
            LOCKON ^FanBox
        ---> S2

    S2, \ Wait for start signal
        #START:
            ON ^HouseLight;
            ON ^LeftLeverOut;
            ON ^RightLeverOut;
            Z1
        ---> S3

    S3, \ Time the session and flag off after Max Time or condition met
        0.01":
            SET T = T + 0.01;
            IF T/60 >= A(^MaxTimeIdx) [@EndSession, @ContinueSession]
                @End: Z32 ---> S4
                @Cont: IF C(^NosepokeCountIdx) >= A(^MaxPelletIdx) [@EndSession]
                    @End: Z32 ---> S4
            ENDIF

    S4, \ Did the subject pass or fail?
        0.01":
            OFF ^LeftLeverOut;
            OFF ^RightLeverOut;
            OFF ^HouseLight;
            IF (C(^LeftLeverCountIdx) + C(^RightLeverCountIdx)) >= A(^RequiredPressIdx) [@Succeed, @Fail]
                @Succ: SET P = 1 ---> S5
                @Fail: SET P = 0 ---> S5

    S5, \ Stop the session
        3":
        ---> STOPABORTFLUSH

\***************************************************
\                Main Program
\***************************************************
S.S.2, \ Experiment logic

    S1, \ Wait for experiment start
        #START:
            SET A(^RateDropIdx) = A(^RateDropIdx) * 1"
        ---> S2

    S2, \ Drop pellets
        A(^RateDropIdx)#T:
            Z2
        ---> S3

        1 #R ^LeftLever:
            SET L(B(^LeftLeverTimeIdx)) = T;
            ADD B(^LeftLeverTimeIdx);
            ADD C(^LeftLeverCountIdx);
            SET L(B(^LeftLeverTimeIdx)) = -987.987;
            SET S = T;
            Z1;
            Z2;
            Z3
        ---> S3

        1 #R ^RightLever:
            SET R(B(^RightLeverTimeIdx)) = T;
            ADD B(^RightLeverTimeIdx);
            ADD C(^RightLeverCountIdx);
            SET R(B(^RightLeverTimeIdx)) = -987.987;
            SET S = T;
            Z1;
            Z2;
            Z4
        ---> S3

    S3, \ Pellet has been dropped, detect right or left presses and nosepoke
        1 #R ^LeftLever:
            SET M(B(^WrongLeftTimeIdx)) = T;
            ADD B(^WrongLeftTimeIdx);
            ADD C(^WrongLeftCountIdx);
            SET M(B(^WrongLeftTimeIdx)) = -987.987;
            Z1
        ---> SX

        1 #R ^RightLever:
            SET N(B(^WrongRightTimeIdx)) = T;
            ADD B(^WrongRightTimeIdx);
            ADD C(^WrongRightCountIdx);
            SET N(B(^WrongRightTimeIdx)) = -987.987;
            Z1
        ---> SX

        1 #R ^Nosepoke:
            SET E(B(^NosepokeTimeIdx)) = T;
            ADD B(^NosepokeTimeIdx);
            ADD C(^NosepokeCountIdx);
            SET E(B(^NosepokeTimeIdx)) = -987.987;
            OFF ^RewardLight;
            Z1
        ---> S2      

S.S.5, \ Detect unnecessary nosepokes and record the times

    S1, \Wait for experiment start
        #START:
        ---> S2

    S2, \ Detect unnecessary nosepokes
        1 #R ^Nosepoke:
            SET O(B(^WrongNosepokeTimeIdx)) = T;
            ADD B(^WrongNosepokeTimeIdx);
            ADD C(^WrongNosepokeCountIdx);
            SET O(B(^WrongNosepokeTimeIdx)) = -987.987;
            Z1
        ---> SX

        #Z2:
        ---> S3
    
    S3, \ Detect correct nosepoke
        1 #R ^Nosepoke:
        ---> S2

S.S.6, \ Detect multiple presses on the same lever

    S1, \ No lever's pressed at this stage
        #Z3:
        ---> S2

        #Z4:
        ---> S5

    S2, \ Left Lever pressed once
        #Z3:
        ---> S3

        #Z4:
        ---> S5

    S3, \ Left Lever pressed twice
        #Z3:
        ---> S4

        #Z4:
        ---> S5

    S4, \ Left Lever pressed thrice
        0.01":
            OFF ^LeftLeverOut
        ---> SX

        #Z4:
            ON ^LeftLeverOut
        ---> S5

    S5, \ Right Lever pressed once
        #Z3:
        ---> S2

        #Z4:
        ---> S6

    S6, \ Right Lever pressed twice
        #Z3:
        ---> S2

        #Z4:
        ---> S7

    S7, \ Right Lever pressed thrice
        0.01":
            OFF ^RightLeverOut
        ---> SX

        #Z3:
            ON ^RightLeverOut
        ---> S2


S.S.7, \ Drop pellets

    S1, \ Drop the pellet
        #Z2:
            ON ^Feeder;
            ON ^RewardLight;
            SET D(B(^FeederTimeIdx)) = T;
            ADD B(^FeederTimeIdx);
            ADD C(^FeederCountIdx);
            SET D(B(^FeederTimeIdx)) = -987.987;
            Z1
        ---> S2

    S2, \ Turn off the feeder after dropping
        0.5": 
            OFF ^Feeder
        ---> S1

S.S.10, \ Update the display

    S1, \ Wait 9.99 seconds or for a Z-Pulse
        9.99":
        ---> S2

        #Z1:
        ---> S2

    S2, \ Update the screen
        .01":
            SHOW 1, Experiment Time (mins), T/60;
            SHOW 2, Pellets, C(^FeederCountIdx);
            SHOW 3, Correct Nosepokes, C(^NosepokeCountIdx);
            SHOW 4, Correct Levers, C(^LeftLeverCountIdx) + C(^RightLeverCountIdx);
            SHOW 5, Time Since Lever (mins), (T - S) / 60; 
            SHOW 6, Correct Lefts, C(^LeftLeverCountIdx);
            SHOW 7, Correct Rights, C(^RightLeverCountIdx);
            SHOW 8, Unnecessary Lefts, C(^WrongLeftCountIdx);
            SHOW 9, Unnecessary Rights, C(^WrongRightCountIdx);
            SHOW 10, Unnecessary Nosepokes, C(^WrongNosepokeCountIdx);
        ---> S1
