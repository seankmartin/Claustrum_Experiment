\***************************************************
\               File Description Comments
\***************************************************

\ Magazine Habituation by SeÃ¡n Martin on 08/07/2019
    \ Brief Description: Drop a pellet every 30 seconds.
    \ Ending criteria: Ends after 15mins (Default)
    \ Locked pins: Remember to turn off the fan (16)

\***************************************************
\               Constant Definitions
\***************************************************
\ Experiment variable locations
    ^MaxTimeIdx = 0
    ^RateDropIdx = 1

\ Index holder locations
    ^FeederTimeIdx = 0
    ^NosepokeTimeIdx = 1

\ Counter locations
    ^FeederCountIdx = 0
    ^NosepokeCountIdx = 1

\ Inputs
    ^Nosepoke = 7

\ Outputs
    ^RewardLight = 6
    ^HouseLight = 7
    ^Feeder = 9
    ^FanBox = 16

\***************************************************
\               Variable Descriptions
\***************************************************

\ Counter descriptions
    \ C(0) = Number of Pellets dropped
    \ C(1) = Number of Nosepokes

\ Aliases
    VAR_ALIAS Maximum Time (min) = A(0) \ Default 15 minutes
    VAR_ALIAS Drop Rate (secs) = A(1) \ Default 30 seconds


\ Arrays
    \ A = Experiment variables
    \ B = Index holders
    \ C = Counters
    \ D = Data array for Feeder drop times
    \ E = Data array for nosepoke times
    DIM A = 1
    DIM B = 1
    DIM C = 1
    DIM D = 120 \ Should be large enough to hold rate * exp_time
    DIM E = 5000 \ Theoretically this could be anything - checks


\ Z Pulses
    \ Z1 = Screen update pulse
    \ Z2 = Activate Feeder
    \ Z32 = End the session

\ Single Variables
    \ T = Elapsed time

DISKVARS = A, B, C, D, E, T

\***************************************************
\               Implementation
\***************************************************

S.S.1, \ Control flow

    S1, \ Establish default values and turn on fan
        0.01": SET A(^MaxTimeIdx) = 15; SET A(^RateDropIdx) = 30; SET D(0) = -987.987; SET E(0) = -987.987; LOCKON ^FanBox ---> S2

    S2, \ Wait for start signal
        #START: ON ^HouseLight; Z1 ---> S3

    S3, \ Time the session and flag off after Max Time
        0.01": SET T = T + 0.01; IF T/60 >= A(^MaxTimeIdx) [@EndSession, @ContinueSession] 
                 @End: Z32 ---> S4
                 @Cont: ---> SX


    S4, \ Stop the session
        3": ---> STOPABORTFLUSH

\***************************************************
\                Main Program
\***************************************************
S.S.2, \ Experiment logic

    S1, \Wait for experiment start
        #START: SET A(^RateDropIdx) = A(^RateDropIdx) * 1" ---> S2

    S2, \ Drop pellets at desired rate
        A(^RateDropIdx)#T: Z2 ---> SX

S.S.5, \ Detect nosepokes and record the times

    S1, \Wait for experiment start
        #START: ---> S2

    S2, \ Detect nosepokes
        1 #R ^Nosepoke: SET E(B(^NosepokeTimeIdx)) = T; ADD B(^NosepokeTimeIdx); ADD C(^NosepokeCountIdx); SET E(B(^NosepokeTimeIdx)) = -987.987; OFF ^RewardLight; Z1 ---> SX

S.S.6, \ Drop pellets

    S1, \ Drop the pellet
        #Z2: ON ^Feeder; ON ^RewardLight; SET D(B(^FeederTimeIdx)) = T; ADD B(^FeederTimeIdx); ADD C(^FeederCountIdx); SET D(B(^FeederTimeIdx)) = -987.987; Z1 ---> S2

    S2, \ Turn off the feeder after dropping
        0.5": OFF ^Feeder ---> S1


S.S.10, \ Update the display

    S1, \ Wait 9.99 seconds or for a Z-Pulse
        9.99": ---> S2

        #Z1: ---> S2

    S2, \ Update the screen
        .01": SHOW 1, Number of Pellets, C(0); SHOW 2, Number of Nosepokes, C(1); SHOW 3, Experiment Time (mins), T / 60 ---> S1
